Option Explicit

Public Cerrar As Boolean
Public listaHojas As Collection
Public Cancelado As Boolean
Public hojasCuadros As Object 'Diccionario cuadros y líneas de alimentación
Public hojasResultados As Object 'Diccionario hojas de resultados
Public hojasIgnorar As Object 'Diccionario hojas a ignorar en selección

Dim Hoja As Workbook
Dim exportPath As String
Dim WS As Worksheet
Dim Rng As Range
Dim rowArr As Variant
Dim i As Long, j As Long, k As Long, cantCuad As Long, colorPag As Long
Dim fNum As Integer, OutPut As Integer
Dim filTabla As Long, primeraFila As Long, primeraColumna As Long, ultimaColumna As Long, ultimaFila As Long
Dim tipoPagina As String, nombrePagina As String
Dim infoAPegar As Collection
Dim DebugMensajes As Boolean, resltTerm As Boolean
Dim colores As Object
Dim key As Variant
'Variables importación
Dim nombreHoja As String, nombreTabla As String
Dim filasTabla As Long, colCount As Long
Dim columnasRellenar As Collection, ConfiguracionCuadros As Collection
Dim tablaConversion As Object


    Dim importPath As Variant
    Dim textoLinea As String
    Dim rowValues() As String
    Dim rowNum As Long
    Dim colNum As Long
    
'Variables barra progreso
Dim progreso As Double, frac As Double, progreso2 As Double, frac2 As Double, veces As Long

Sub Exportar()
    On Error GoTo ErrorHandler

    Set Hoja = ActiveWorkbook

    'Pedir usuario ruta de guardado de archivo
    exportPath = Application.GetSaveAsFilename( _
        InitialFileName:=Format(Now(), "yymmdd") & "_exportación.dat", _
        FileFilter:="Archivos de datos (*.dat), *.dat, Todos los archivos (*.*), *.*", _
        Title:="Guardar archivo de datos")
    'Cancelado o cerrado
    On Error GoTo Seleccionado1
    If exportPath = False Then
        MsgBox "Exportación cancelada.", vbExclamation
        Exit Sub
    End If
Seleccionado1:
    On Error GoTo ErrorHandler  'Reiniciar gestión de errores

    'Crear colecciones y diccionarios para pasar datos
    Set listaHojas = New Collection
    Set hojasCuadros = CreateObject("Scripting.Dictionary")
    Set hojasResultados = CreateObject("Scripting.Dictionary")
    Set hojasIgnorar = CreateObject("Scripting.Dictionary")

    'Llenar colección con nombres de hojas
    For Each WS In Hoja.Worksheets
        listaHojas.Add WS.name
    Next WS

    'Dejar a usuario elegir hojas de cuadros y resultados con formulario
    Cerrar = False
    Cancelado = False
    Seleccion.Show vbModeless

    'Esperar a que se cierre formulario
    Do While Not Cerrar
        DoEvents
    Loop

    If Cancelado Then
        Close #fNum
        Exit Sub
    End If

    Call IniciarBarra
    ufProgressObr.Caption = "Exportación de datos"
    ufProgressObr.Proceso.Caption = "Iniciando.."
    ufProgressObr.SubProceso.Caption = "Abriendo archivo.."

    cantCuad = hojasCuadros.Count + hojasResultados.Count

    progreso = 1 / (cantCuad + 3)
    progreso2 = 1

    'Crear archivo de texto e imprimir datos
    fNum = FreeFile
    Open exportPath For Output As #fNum

    Call ActualizarBarra

    ufProgressObr.Proceso.Caption = "Construyendo información general de cuadros.."
    ufProgressObr.SubProceso.Caption = "Haciendo cosas.."

    Set ConfiguracionCuadros = New Collection
    'Construir array con datos de configuración de cuadros (nombre cuadro, línea alimentación, item, nombre línea alimentación si es externa, nº circuito)
    For Each key In hojasCuadros.Keys
        Dim configCuadro(1 To 6) As String  'Array para almacenar datos de configuración de cada cuadro

        configCuadro(1) = Hoja.Sheets(key).Range("E1").Value    'Nombre cuadro
        configCuadro(2) = key 'Abreviatura cuadro
    
        If hojasCuadros(key)(0) = "EXTERNO ------>" Then
            configCuadro(3) = hojasCuadros(key)(0)
            configCuadro(4) = hojasCuadros(key)(2) 'Item línea alimentación
            configCuadro(5) = hojasCuadros(key)(1) 'Nombre línea alimentación externo
        Else
            configCuadro(3) = hojasCuadros(key)(2) & " - " & hojasCuadros(key)(1) 'Nombre línea alimentación con formato "item - nombre" si es interno
            configCuadro(4) = ""
            configCuadro(5) = ""
        End If
        configCuadro(6) = ""

        ConfiguracionCuadros.Add configCuadro
    Next key

    'Generar formato de configuracion cuadros
    Print #fNum, "[FSheet]|" & "Configuracion_cuadros" & "|" & hojasCuadros.Count & "|" & "2315831"

    For i = 1 To ConfiguracionCuadros.Count
        For j = 1 To 6 'Número de datos de configuración por cuadro
            Print #fNum, ConfiguracionCuadros(i)(j) & "|";
        Next j
        Print #fNum,   'Nueva fila
    Next i
    'Fin de configuración cuadros
    Print #fNum, "-----"

    For Each WS In Hoja.Worksheets  'Primero hoja de resultados
        If hojasResultados.Exists(WS.name) And Not resltTerm Then
            ufProgressObr.Proceso.Caption = "Procesando hoja: " & WS.name & " (" & cantCuad & " hojas restantes)"
            tipoPagina = "Fijo"
            nombrePagina = "TABLA DE RESULTADOS"
            colorPag = WS.Tab.color

            Call MeterEnArchivo(tipoPagina, nombrePagina, colorPag)
            ufProgressObr.Denbora.Caption = ""
            resltTerm = True
        End If
    Next WS

    'Exportar páginas
    For Each WS In Hoja.Worksheets    
        ufProgressObr.Proceso.Caption = "Procesando hoja: " & WS.name & " (" & cantCuad & " hojas restantes)"

        If hojasCuadros.Exists(WS.name) Then
            tipoPagina = "Generado"
            nombrePagina = WS.name
            colorPag = WS.Tab.color

            Call MeterEnArchivo(tipoPagina, nombrePagina, colorPag)
            ufProgressObr.Denbora.Caption = ""
        End If
    Next WS

    ufProgressObr.Proceso.Caption = "Exportación completada."
    ufProgressObr.SubProceso.Caption = "Cerrando macro.."

    Close #fNum
    MsgBox "Hoja exportada a archivo: " & exportPath & vbCrLf _
        & "Listo para importarse en otra hoja.", vbInformation

    Unload ufProgressObr
    Exit Sub
ErrorHandler:
    'Si ocurre algun error ejecutar
    MsgBox "Ha ocurrido un error al exportar los datos: " & Err.Description, vbExclamation, "Error 1001"
    'Activar actualización de pantalla
    Application.ScreenUpdating = True
    'Activar calculacion automatica
    Application.Calculation = xlCalculationAutomatic
    Application.DisplayAlerts = True
    Application.EnableEvents = True
    Close #fNum
    Unload ufProgressObr
    End
End Sub

Private Sub MeterEnArchivo(ByVal tipP As String, ByVal nomP As String, ByVal color As Long)
    ufProgressObr.SubProceso.Caption = "Cargando parámetros de hoja.."
    'Diccionario
    Set tablaConversion = CreateObject("Scripting.Dictionary")
    Set WS = Hoja.Worksheets(nomP)
    'Tablas conversión y variables por hoja
    Select Case tipP
        Case "Fijo" 'Resultados
            'Tabla de conversión para tabla de resultados
            tablaConversion.Add 7, 8    'Tipo instalación (A1, A2, B, E...)
            tablaConversion.Add 16,13   'Secc elegida
            tablaConversion.Add 17,9    'Aislamiento (RZ1-K, SZ1-K, H07Z1-K...)
            tablaConversion.Add 18,10   'Agrupamiento
            tablaConversion.Add 19,11   'Cable por fase
            tablaConversion.Add 20,12   'Manguera
            tablaConversion.Add 21,14   'Material
            tablaConversion.Add 24,19   'PIA
            tablaConversion.Add 26,20   'Longitud
            tablaConversion.Add 28,25   'Tipo instalación (Sup., int., ext...)
            tablaConversion.Add 42,"E1" 'Nombre cuadro
            tablaConversion.Add 43,"E2" 'Fila
            tablaConversion.Add 44,"E3" 'Línea alimentación
            tablaConversion.Add 51, "F" 'Última columna para importación sin errores

            'Filas y columnas de tabla de resultados
            primeraFila = 13
            primeraColumna = 1
            ultimaColumna = 28

            ultimaFila = WS.Cells(WS.Rows.count, 1).End(xlUp).Row
        Case "Generado" 'Cuadros a generar
            tablaConversion.Add 1, 1  'Denominación circuito
            tablaConversion.Add 2, 2  'Nombre circuito
            tablaConversion.Add 4, 4  'Nº elemento
            tablaConversion.Add 5, 5  'Tipo
            tablaConversion.Add 6, 6  'Potencia
            tablaConversion.Add 10,10 'Factor simultaneidad
            tablaConversion.Add 11,11 'Factor utilidad
            tablaConversion.Add 13,13 'Monofasica o trifasica
            tablaConversion.Add 14,14 'Fase

            'Filas y columnas de cuadros a generar
            primeraFila = 4
            primeraColumna = 1
            ultimaColumna = 14

            ultimaFila = WS.Cells(WS.Rows.count, 1).End(xlUp).Row
        Case Else
            Exit Sub
    End Select

    filTabla = ultimaFila - primeraFila + 1

    progreso2 = 1 / (filTabla + 3) 'Progreso secundario por cada fila de tabla exportada
    Call ActualizarBarra2
    ufProgressObr.SubProceso.Caption = "Pasando tabla a memoria.."
    
    Set Rng = WS.Range(WS.Cells(primeraFila, primeraColumna), WS.Cells(ultimaFila - 1, ultimaColumna))

    ' MsgBox rng.Address
    rowArr = Rng.Value

    Dim prim As Boolean
    Dim fila As Long
    fila = 4
    prim = True

    Call ActualizarBarra2

    'Título de página
    Select Case tipP
        Case "Fijo"
            Print #fNum, "[FSheet]|" & nomP & "|" & filTabla & "|" & color
        Case "Generado"
            Print #fNum, "[GSheet]|" & nomP & "|" & filTabla & "|" & color
        Case Else
            Print #fNum, "[Sheet]|" & nomP & "|" & filTabla & "|" & color
    End Select

    'Valores
    For i = 1 To UBound(rowArr, 1)  'Recorrer filas
        ufProgressObr.SubProceso.Caption = "Exportando tabla a archivo. Fila: " & i & "/" & filTabla
        'Extraer fórmula de columna B para uso en tabla de resultados
        Dim formula As String
        Dim hojaForm As Variant, hojaFormAnt As Variant
        formula = WS.Cells(i + primeraFila - 1, 2).Formula
    
        If InStr(formula, "&") > 0 Then 'Para casos en los que la fórmula contiene texto como "Cuadro"..
            formula = Trim(Mid(formula, InStr(formula, "&") + 1))
        Else
            formula = Trim(formula)
        End If

        Dim partesFormula() As String   'Separar fórmula por "!" para extraer nombre de hoja de cuadro al que hace referencia, que se usará en tabla de resultados para identificar a qué cuadro corresponde cada línea de alimentación
        partesFormula = Split(formula, "!")
        If UBound(partesFormula) >= 1 Then
            hojaForm = Replace(partesFormula(0), "='", "")
            hojaForm = Replace(hojaForm, "'", "")
            hojaForm = Replace(hojaForm, "=", "")   'Si queda un signo de igual
        Else
            hojaForm = "ini"
        End If

        hojaFormAnt = IIf(hojaFormAnt = "", hojaForm, hojaFormAnt)  'Inicializar hojaFormAnt en la primera iteración

        'Comprobar si la hoja de cuadro extraída de la fórmula es diferente a la hoja de cuadro de la fila anterior, para actualizar el valor de línea de alimentación en caso de que haya cambiado de cuadro dentro de la misma hoja de resultados
        If hojaForm <> hojaFormAnt Then
            fila = 4 'Reiniciar contador de fila de alimentación al cambiar de cuadro
            prim = True 'Reiniciar variable para dejar en blanco la primera fila de cada nuevo cuadro en la columna de fila de alimentación
            hojaFormAnt = hojaForm 'Actualizar hoja de cuadro anterior
            DoEvents
        End If

        For j = 1 To tablaConversion.Keys()(tablaConversion.Count - 1)  'Valores de cada fila columna por columna
            'On Error Resume Next    'Cuando j supere el numero de columnas de rowArr, para evitar error de subíndice fuera de rango y seguir exportando el resto de columnas que sí existen
            If j <= UBound(rowArr, 2) Then
                If IsNumeric(rowArr(i, j)) And rowArr(i, j) <> " " And rowArr(i, j) <> "" Then
                    rowArr(i, j) = Replace(rowArr(i, j), ",", ".")  'Comas a puntos para correcta valoración de decimales
                End If
            End If

            'Aplicar tabla de conversión
            Dim rowArr2 As Variant
            ReDim rowArr2(1 To UBound(rowArr, 1), 1 To tablaConversion.Keys()(tablaConversion.Count - 1))

            ufProgressObr.Denbora.Caption = "Cuadro: " & hojaForm & " - Fila alimentación: " & fila

            If tablaConversion.Exists(j) Then   'Si la columna actual está en la tabla de conversión, poner valor de la columna correspondiente según tabla de conversión
                For Each key In tablaConversion.Keys
                    If key = j Then
                        If IsNumeric(tablaConversion(key)) Then 'Or tablaConversion(key) = "" 
                            rowArr2(i, j) = rowArr(i, tablaConversion(key))  'Valor convertido según tabla de conversión
                        Else
                            'No es numérico, conseguir últimas 3 variables para tabla de resultados (nombre cuadro, fila y línea alimentación) según tabla de conversión
                            Select Case tablaConversion(key)
                                Case "E1" 'Abreviatura cuadro
                                        If hojaForm = "ini" Then
                                            rowArr2(i, j) = "ALIM"
                                        Else
                                            rowArr2(i, j) = hojaForm
                                        End If
                                Case "E2" 'Fila
                                    If prim Then
                                        rowArr2(i, j) = "" 'Dejar en blanco la primera fila de línea de alimentación, que corresponde a los encabezados de la tabla
                                    Else
                                        rowArr2(i, j) = fila
                                        fila = fila + 1
                                    End If
                                Case "E3" 'Línea alimentación
                                    If prim Then
                                        rowArr2(i, j) = "" 'Dejar en blanco la primera fila de línea de alimentación, que corresponde a los encabezados de la tabla
                                    Else
                                        If hojaForm = "ini" Then
                                            rowArr2(i, j) = "ALIM"
                                        Else
                                            rowArr2(i, j) = hojasCuadros(hojaForm)(2) 'Valor del item de línea de alimentación según formulario de selección de cuadros
                                        End If
                                    End If
                                Case "F"
                                    rowArr2(i, j) = "" 'Columna para importación sin errores, se deja en blanco porque no se necesita exportar ningún valor en esta columna, solo se usa para evitar errores de importación al haber una columna más que en la tabla de resultados original
                            End Select
                        End If
                        Exit For
                    End If
                Next key
            Else
                rowArr2(i, j) = ""  'Valor en blanco, no se necesita para exportación
            End If

            'Escribir valor de fila en archivo
            Print #fNum, rowArr2(i, j) & "|";
        Next j
        prim = False
        Print #fNum,   'Nueva fila
        Call ActualizarBarra2
    Next i

    Print #fNum, "-----"

    cantCuad = cantCuad - 1
    Call ReiniciarBarra2
End Sub

'===============================================================================================================================================
'Barra de progreso
'===============================================================================================================================================
Private Sub ActualizarBarra()
    'Fracción por cada llamada
    frac = progreso + frac
    'Actualizar barra de progreso
    With ufProgressObr
        .LabelProgress.Width = frac * (.FrameProgress.Width)
        .Repaint
    End With
    DoEvents
    veces = veces + 1
End Sub

Private Sub ActualizarBarra2()
    'Fracción por cada llamada
    ' Application.Wait Now + #12:00:01 AM#
    frac2 = progreso2 + frac2
    'Actualizar barra de progreso secundaria
    With ufProgressObr
        .LabelProgress2.Width = frac2 * (.FrameProgress2.Width)
    End With
    DoEvents
End Sub

Private Sub ReiniciarBarra2()
    'Reiniciar barra de progreso secundaria
    ' Application.Wait Now + #12:00:01 AM#
    'SubProceso.Caption = ""
    ufProgressObr.LabelProgress2.Width = 0
    frac2 = 0
    progreso2 = 0
    'Avanzar la barra de progreso primaria
    Call ActualizarBarra
End Sub


Private Sub IniciarBarra()
    'Variables y textos
    frac = 0
    frac2 = 0
    ufProgressObr.TituloObra.Caption = ""
    ufProgressObr.LabelProgress.Width = 0
    ufProgressObr.LabelProgress2.Width = 0
    ufProgressObr.Caption = ""
    ufProgressObr.Proceso.Caption = ""
    ufProgressObr.SubProceso.Caption = "Iniciando barra.."
    ufProgressObr.TiempoEstimado.Caption = ""
    ufProgressObr.Denbora.Caption = ""
    ufProgressObr.Show vbModeless
    'Variable de parada
    Cerrar = False
End Sub