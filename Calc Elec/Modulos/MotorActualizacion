Option Explicit

Dim DebugMensajes As Boolean
Dim vbOut As Integer
Dim RutaUsuario As String, RutaLicencia As String, RutaLogsUsuario As String
Dim ExcelActualizado As Workbook, ExcelLocal As Workbook
Dim rutaArchivo As String
Dim nombreArchivo As String
Dim versionS As String
Dim i As Integer, reintentar As Integer

Dim VBProj As VBIDE.VBProject
Dim VBComp As VBIDE.VBComponent, VBCompL As VBIDE.VBComponent

Dim FromVBProject As VBIDE.VBProject
Dim ToVBProject As VBIDE.VBProject

Dim nomModulo As Collection, nomModuloB As Collection

Dim progreso As Double
Dim frac As Double

Dim fso As Object

Dim modInicio As Long, modFinal As Long
Dim correcto As Long

Dim errorConexion As Boolean, actualizado As Boolean, completado As Boolean
Dim VersionActual As String
Dim NumVersion As String
Dim FechaVersion As String
Dim versionPuntos As String

Public Cerrar As Boolean

'Rutas constantes independiente de usuario
Private Const RutaServidor As String = "\BOSLAN INGENIERIA Y CONSULTORIA\Instalaciones - Imagenes\98-PROGRAMAS INST\Gontzal proba"
Private Const RutaBase As String = RutaServidor & "\Proyectos"
Private Const RutaLogs As String = RutaBase & "\Logs"

Private Const RutaExcel As String = "\CuadrosBT(Pr2)\CAL ELEC(mas actualizado).xlsm"
Private Const Proyecto As Long = 2

'Variables para logging
Private Const PERMANENT_LOG As String = "RegistroPr" & Proyecto & ".log"

Private tempLogPath As String
Private permanentLogPath As String

Private permanentFileHandle As Integer
Private tempFileHandle As Integer
Private logsInitialized As Boolean

'===========================================================
' Procedimiento principal de actualización
'===========================================================
Sub Actualizar2()
    ' DebugMensajes = ThisWorkbook.Sheets("Busquedas").Range("Debug")
    ' If Not DebugMensajes Then On Error GoTo ErrorHandler

    'Desactivar actulaizaciones de pantalla
    Application.ScreenUpdating = False
    'Desactivar calculacion automatica
    Application.Calculation = xlCalculationManual
    'Borrar portapapeles
    Application.CutCopyMode = False
    'Desactivar eventos
    Application.EnableEvents = False

    'Número de reintentos automaticos
    reintentar = 5
Reintentar1:
    'Rutas
    RutaUsuario = Environ("UserProfile")

    Call ComprobarActualizacionIntegrado("A")
    'Reintentar si hay error de conexion
    If errorConexion Then
        reintentar = reintentar - 1
        If reintentar > 0 Then
            GoTo Reintentar1
        Else
            MsgBox "No se ha podido establecer contacto con el servidor tras varios intentos.", vbExclamation, "Error de conexión"
            Exit Sub
        End If
    End If

    'Comparar versión actual y nueva
    If Not actualizado Then
        vbOut = MsgBox("Hay mas actualizado, actualizar a última versión?" & vbCrLf & _
            "Fecha de nueva versión: " & FechaVersion, vbYesNo + vbInformation, "Actualización disponible")
        If vbOut = 7 Then Exit Sub   'No
    Else
        vbOut = MsgBox("No hay actualizaciones disponibles." & vbCrLf & "Actualizar aún así?. Fecha de nueva versión: " _
            & FechaVersion, vbYesNo + vbInformation, "Sin actualizaciones")
        If vbOut = 7 Then Exit Sub   'No
    End If

    Set ExcelLocal = ActiveWorkbook

    'Empezar logging
    Call EmpezarLog
    LogWriteTemp "Empezado proceso de actualización"

    'Contar módulos
    modInicio = 0
    For Each VBComp In ExcelLocal.VBProject.VBComponents  'Contar módulos con código
        modInicio = modInicio + 1
    Next VBComp

    LogWriteTemp "Módulos contados en local: " & modInicio

    'Iniciar colecciones
    Set nomModulo = New Collection
    Set nomModuloB = New Collection

    'Iniciar el fso
    Set fso = CreateObject("Scripting.FileSystemObject")

    LogWriteTemp "FSO y colecciones de módulos iniciados."

    Call IniBarra
    'Textos
    ufProgress.Caption = "Actualizar código"
    ufProgress.LabelCaption.Caption = "Generando ruta a servidor.." & vbCrLf & _
        "Por favor, no abra o seleccione otro documento de Excel o Word."
    DoEvents

    'Comprobar que archivo existe en servidor
    If fso.FileExists(RutaUsuario & RutaBase & RutaExcel) Then
        If DebugMensajes Then
            MsgBox "Archivo actualizado encontrado en: " & RutaUsuario & RutaBase & RutaExcel
        End If
    Else
        MsgBox "No se ha podido establecer conexión con el servidor." & RutaUsuario & RutaBase & RutaExcel, vbCritical
        Unload ufProgress
        'Gestión de logs
        LogWrite "Sin conexión al servidor."
        LogWriteTemp "No se ha podido borrar/importar módulo: " & nomModulo(i)
        CloseLogs success:=False
        Exit Sub
    End If

    ufProgress.LabelCaption.Caption = "Estableciendo conexión con servidor.." & vbCrLf & _
        "Por favor, no abra o seleccione otro documento de Excel o Word."
    DoEvents

    Set ExcelActualizado = Workbooks.Open(RutaUsuario & RutaBase & RutaExcel)   'Abrir libro actualizado
    ExcelActualizado.Windows(1).Visible = False 'Esconder libro

    If Cerrar Then GoTo Salir

    LogWriteTemp "Conexión establecida con servidor."

    ufProgress.LabelCaption.Caption = "Contando módulos a actualizar.." & vbCrLf & _
        "Por favor, no abra o seleccione otro documento de Excel o Word."
    DoEvents

    Set VBProj = ExcelActualizado.VBProject 'Proyecto en servidor

    For Each VBComp In VBProj.VBComponents  'Crear lista de módulos en servidor
        If Cerrar Then GoTo Salir

        If TotalCodeLinesInVBComponent(VBComp) > 0 Then
            'Solo módulos con código
            nomModulo.Add VBComp.name
            If DebugMensajes Then MsgBox "Módulo encontrado en servidor: " & VBComp.name & " Líneas: " & TotalCodeLinesInVBComponent(VBComp)
        End If
    Next VBComp

    LogWriteTemp "Módulos contados en servidor: " & nomModulo.Count

    Set VBProj = ExcelLocal.VBProject 'Proyecto de usuario

    For Each VBComp In VBProj.VBComponents  'Crear lista de módulos en archivo base
        If Cerrar Then GoTo Salir

        If TotalCodeLinesInVBComponent(VBComp) > 0 Then
            'Solo módulos con código
            nomModuloB.Add VBComp.name
            If DebugMensajes Then MsgBox "Módulo encontrado en base: " & VBComp.name & " Líneas: " & TotalCodeLinesInVBComponent(VBComp)
        End If
    Next VBComp

    LogWriteTemp "Módulos contados en base: " & nomModuloB.Count

    'Cuantas veces se llama a actualizar barra
    progreso = 1 / (nomModulo.Count + 5) ' +5 por las llamadas extra

    If DebugMensajes Then MsgBox nomModulo.Count

    Call ActBarra

    ufProgress.LabelCaption.Caption = "Extrayendo ruta de archivo.." & vbCrLf & _
        "Por favor, no abra o seleccione otro documento de Excel o Word."

    'Conseguir ruta de archivo actual
    rutaArchivo = ExcelLocal.path
    If DebugMensajes Then MsgBox "Ruta de archivo actual: " & rutaArchivo

    Call ActBarra

    'Conseguir nombre de archivo actual para borrar al finalizar
    ufProgress.LabelCaption.Caption = "Extrayendo nombre de archivo.." & vbCrLf & _
    "Por favor, no abra o seleccione otro documento de Excel o Word."

    nombreArchivo = ExcelLocal.name

    If DebugMensajes Then MsgBox "Nombre de archivo actual: " & nombreArchivo

    Call ActBarra

    'Libros para copiar módulos
    Set FromVBProject = ExcelActualizado.VBProject
    Set ToVBProject = ExcelLocal.VBProject

    If Cerrar Then GoTo Salir

    LogWriteTemp "Iniciando proceso de actualización de módulos."

    'Eliminar todos los modulos excepto el de actualización
    Err.Clear
    On Error Resume Next

    For i = 1 To nomModuloB.Count
        ufProgress.LabelCaption.Caption = "Borrando módulo " & i & " de " & nomModuloB.Count & vbCrLf & _
        "Por favor, no abra o seleccione otro documento de Excel o Word."

        Set VBComp = ToVBProject.VBComponents(nomModuloB(i))

        If Cerrar Then GoTo Salir
        reintentar = 1
        If nomModuloB(i) = "MotorActualizacion" Or nomModuloB(i) = "ufProgress" Or ComponentTypeToString(VBComp.Type) = "Document Module" Then
            'Saltar módulo de actualización para evitar errores
            If DebugMensajes Then MsgBox "Saltando módulo: " & nomModuloB(i) & " tipo " & ComponentTypeToString(VBComp.Type)
            completado = True
        Else
Reintentar:
            ufProgress.TiempoEstimado.Caption = "Eliminando módulo: " & nomModuloB(i)

            ToVBProject.VBComponents.Remove ToVBProject.VBComponents(nomModuloB(i))
            DoEvents

            'Comprobar si se ha borrado
            Set VBComp = Nothing
            Set VBComp = ToVBProject.VBComponents(nomModuloB(i))

            If VBComp Is Nothing Then
                completado = True
            Else
                'Existe todavía, fallo al borrar
                completado = False
            End If
        End If

        '3 intentos para borrar
        If Not completado And reintentar < 4 Then 
            ufProgress.TiempoEstimado.Caption = "Error al borrar el módulo: " & nomModuloB(i) & " Reintento nº " & reintentar
            reintentar = reintentar + 1

            DoEvents
            GoTo Reintentar
        ElseIf Not completado And reintentar = 4 Then
            If DebugMensajes Then MsgBox "Error al borrar el módulo: " & nomModuloB(i), vbCritical
            GoTo Fallo 'Salir si hay error
        End If
        If Cerrar Then GoTo Salir
        ufProgress.TiempoEstimado.Caption = ""
    Next i
    Call ActBarra

    LogWriteTemp "Módulos borrados en local."

    'Importar módulos
    For i = 1 To nomModulo.Count
        ufProgress.LabelCaption.Caption = "Actualizando módulo " & i & " de " & nomModulo.Count & vbCrLf & _
        "Por favor, no abra o seleccione otro documento de Excel o Word."

        If Cerrar Then GoTo Salir

        If nomModulo(i) = "MotorActualizacion" Or nomModulo(i) = "ufProgress" Then
            'Saltar módulo de actualización para evitar errores
            completado = True
        ElseIf TotalCodeLinesInVBComponent(FromVBProject.VBComponents(nomModulo(i))) > 0 Then
            'Si tiene código, importar
            ufProgress.TiempoEstimado.Caption = "Importando módulo: " & nomModulo(i)
            completado = CopyModule(nomModulo(i), FromVBProject, ToVBProject, True)
            DoEvents
        End If

        Call ActBarra

        If Not completado Then 
            If DebugMensajes Then MsgBox "Error al actualizar el módulo: " & nomModulo(i), vbCritical
            GoTo Fallo 'Salir si hay error
        End If
    Next i

    LogWriteTemp "Módulos importados en local."

    ufProgress.LabelCaption.Caption = "Verificando actualización.." & vbCrLf & _
    "Por favor, no abra o seleccione otro documento de Excel o Word."
    ufProgress.TiempoEstimado.Caption = ""
    correcto = 0

    For Each VBComp In FromVBProject.VBComponents  'Módulos en servidor
        If TotalCodeLinesInVBComponent(VBComp) > 0 Then
            For Each VBCompL In ToVBProject.VBComponents  'Módulos en local

                If Cerrar Then GoTo Salir

                If TotalCodeLinesInVBComponent(VBCompL) > 0 And VBCompL.name = VBComp.name And VBCompL.name <> "MotorActualizacion" Then    'Ignorar módulo de actualización
                    'Si existe, sumar
                    If TotalCodeLinesInVBComponent(VBCompL) = TotalCodeLinesInVBComponent(VBComp) Then
                        'Si tienen mismo número de líneas, continuar
                        correcto = correcto + 1
                        If DebugMensajes Then
                            MsgBox "Módulo verificado correctamente: " & VBComp.name & vbCrLf & _
                                "Líneas en servidor: " & TotalCodeLinesInVBComponent(VBComp) & vbCrLf & _
                                "Líneas en local: " & TotalCodeLinesInVBComponent(VBCompL), vbInformation
                        End If
                    Else
                        MsgBox "Error al verificar el módulo: " & VBComp.name & vbCrLf & _
                            "Líneas en servidor: " & TotalCodeLinesInVBComponent(VBComp) & vbCrLf & _
                            "Líneas en local: " & TotalCodeLinesInVBComponent(VBCompL), vbCritical
                        completado = False

                        LogWriteTemp "Error de verificación en módulo: " & nomModulo(i)

                        Exit For
                    End If
                    ufProgress.TiempoEstimado.Caption = "Verificando módulo: " & VBComp.name & " " & correcto & "/" & nomModulo.Count - 1 & " correctos."
                    DoEvents
                    Exit For
                End If
            Next VBCompL
        End If
    Next VBComp

    ufProgress.LabelCaption.Caption = "Finalizando actualización.." & vbCrLf & _
    "Por favor, no abra o seleccione otro documento de Excel o Word."
    ufProgress.TiempoEstimado.Caption = correcto & "/" & nomModulo.Count - 1 & " correctos."

    ExcelActualizado.Close SaveChanges:=False 'Cerrar libro actualizado sin guardar

    LogWriteTemp "Verificación de módulos completada. Módulos correctos: " & correcto & "/" & nomModulo.Count - 1   '-1 por el de actualización
    LogWriteTemp "Archivo de servidor cerrado."

    ufProgress.LabelCaption.Caption = "Actualizando versión.." & vbCrLf & _
    "Por favor, no abra o seleccione otro documento de Excel o Word."

    'Actualizar versión de programa
    versionS = VersionCodigoIntegrado()
    ExcelLocal.Sheets("Configuracion_cuadros").Range("Version").Value = versionPuntos

    LogWriteTemp "Versión actualizada a: " & versionPuntos

    'Descargar barra
    Unload ufProgress

    Set ExcelLocal = ActiveWorkbook

    'Contar módulos
    modFinal = 0
    For Each VBComp In ExcelLocal.VBProject.VBComponents  'Contar módulos con código
        'MsgBox VBComp.name
        modFinal = modFinal + 1
    Next VBComp
Fallo:
    If completado Then
        ExcelLocal.Sheets("Configuracion_cuadros").Range("AvisoActualizacion") = "El programa está actualizado."
        MsgBox "Actualización completada con éxito." & vbCrLf & _
            "Módulos actualizados: " & nomModulo.Count - 1 & vbCrLf & _
            "Módulos antes de actualización: " & modInicio & vbCrLf & _
            "Módulos después de actualización: " & modFinal, vbInformation
        'Gestión de logs
        LogWrite "Actualizado correctamente a versión: " & versionPuntos
        CloseLogs success:=True
    Else
        ExcelLocal.Sheets("Configuracion_cuadros").Range("AvisoActualizacion") = "Actualización fallida, intentar de nuevo."
        'Gestión de logs
        LogWrite "Actualización fallida."
        LogWriteTemp "No se ha podido borrar/importar módulo: " & nomModulo(i)
        CloseLogs success:=False
        'Activar actulaizaciones de pantalla
        Application.ScreenUpdating = True
        'Activar calculacion automatica
        Application.Calculation = xlCalculationAutomatic
        'Activar eventos
        Application.EnableEvents = True
        ExcelActualizado.Close SaveChanges:=False 'Cerrar libro actualizado sin guardar
        Unload ufProgress
        vbOut = MsgBox("Error al actualizar el archivo, abrir registro?.", vbCritical + vbYesNo, "Error de actualización")
        If vbOut = 6 Then
            'Abrir log temporal
            Dim logFile As Object
            Set logFile = fso.OpenTextFile(tempLogPath, 1)
            ' Dim logContent As String
            ' logContent = logFile.ReadAll
            ' logFile.Close
            ' MsgBox logContent, vbOKOnly, "Registro de actualización"        
        End If
    End If

    'Activar actulaizaciones de pantalla
    Application.ScreenUpdating = True
    'Activar calculacion automatica
    Application.Calculation = xlCalculationAutomatic
    'Activar eventos
    Application.EnableEvents = True
    Set fso = Nothing
Exit Sub

Salir:
    'Gestión de logs
    LogWrite "Actualización cancelada por usuario."
    LogWriteTemp "Operación cancelada."
    CloseLogs success:=False
    'Activar actulaizaciones de pantalla
    Application.ScreenUpdating = True
    'Activar calculacion automatica
    Application.Calculation = xlCalculationAutomatic
    'Activar eventos
    Application.EnableEvents = True

    Set fso = Nothing
    Unload ufProgress
    ExcelActualizado.Close SaveChanges:=False 'Cerrar libro actualizado sin guardar
    MsgBox "Actualización cancelada por el usuario.", vbInformation, "Actualización cancelada"
    End

ErrorHandler:
    'Si ocurre algun error ejecutar
    'Gestión de logs
    LogWrite "Error encontrado en actualización."
    LogWriteTemp "Actualización interrumpida por error: " & Err.Description
    CloseLogs success:=False
    MsgBox "Se ha producido un error a la hora de actualizar el excel: " & Err.Description, vbOKOnly + vbExclamation, "Error 102"
    'Activar actulaizaciones de pantalla
    Application.ScreenUpdating = True
    'Activar calculacion automatica
    Application.Calculation = xlCalculationAutomatic
    'Activar eventos
    Application.EnableEvents = True
    'Descargar barra
    Unload ufProgress
    Set fso = Nothing
    ExcelActualizado.Close SaveChanges:=False 'Cerrar libro actualizado sin guardar
    End
End Sub

'===========================================================
' Código de logging
'===========================================================
Sub EmpezarLog()
    Dim mensaje As String

    If logsInitialized Then Exit Sub

    RutaLogsUsuario = RutaUsuario & RutaLogs
    If Dir(RutaLogsUsuario, vbDirectory) = "" Then MkDir RutaLogsUsuario 'Crear carpeta de logs si no existe
    permanentLogPath = RutaLogsUsuario & "\" & PERMANENT_LOG
    'Crear rutas de logs temporales
    tempLogPath = RutaLogsUsuario & "\TempLog_Pr" & Proyecto & "_" & Format(Now, "yyyy.mm.dd_hh.nn.ss") & ".txt"
    'Abrir log permanente en modo append
    permanentFileHandle = FreeFile
    Open permanentLogPath For Append As #permanentFileHandle
    'Crear y abrir log temporal en modo output
    tempFileHandle = FreeFile
    Open tempLogPath For Output As #tempFileHandle
    logsInitialized = True

    Print #permanentFileHandle, "===" & Format(Now, "YYYY/MM/DD hh:nn:ss") & " - Comienzo actualización ===" 'Encabezado log permanente
    LogWrite "Usuario: " & Environ("Username")
    LogWrite "Ruta: " & ExcelLocal.FullName

    Print #tempFileHandle, Format(Now, "YYYY/MM/DD hh:nn:ss") & " - Inicializado registro." 'Encabezado log temporal
    LogWriteTemp "Usuario: " & Environ("Username")
    LogWriteTemp "Ruta: " & ExcelLocal.FullName
End Sub

'Log permanente
Sub LogWrite(msg As String)
    If logsInitialized Then
        Print #permanentFileHandle, Format(Now, "hh:nn:ss") & " | " & msg
        DoEvents
    End If
End Sub

'Log temporal
Sub LogWriteTemp(msg As String)
    If logsInitialized Then
        Print #tempFileHandle, Format(Now, "hh:nn:ss") & " | " & msg
        DoEvents
    End If
End Sub

'Cerrar logs
Public Sub CloseLogs(Optional ByVal success As Boolean = False)
    If Not logsInitialized Then Exit Sub

    'Cerrar archivos de log
    Close #permanentFileHandle
    Close #tempFileHandle

    If success Then 'Borrar log temporal si ha ido bien
        On Error Resume Next
        Kill tempLogPath
    End If

    logsInitialized = False
End Sub

'===========================================================
' Funciones core de actualización, modificado desde (http://www.cpearson.com/excel/vbe.aspx)
'===========================================================
Function CopyModule(ModuleName As String, _
    FromVBProject As VBIDE.VBProject, _
    ToVBProject As VBIDE.VBProject, _
    OverwriteExisting As Boolean) As Boolean
    '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
    ' CopyModule
    ' This function copies a module from one VBProject to
    ' another. It returns True if successful or  False
    ' if an error occurs.
    '
    ' Parameters:
    ' --------------------------------
    ' FromVBProject         The VBProject that contains the module
    '                       to be copied.
    '
    ' ToVBProject           The VBProject into which the module is
    '                       to be copied.
    '
    ' ModuleName            The name of the module to copy.
    '
    ' OverwriteExisting     If True, the VBComponent named ModuleName
    '                       in ToVBProject will be removed before
    '                       importing the module. If False and
    '                       a VBComponent named ModuleName exists
    '                       in ToVBProject, the code will return
    '                       False.
    '
    '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

    Dim FName As String
    Dim CompName As String
    Dim s As String
    Dim SlashPos As Long
    Dim ExtPos As Long
    Dim TempVBComp As VBIDE.VBComponent

    '''''''''''''''''''''''''''''''''''''''''''''
    ' Do some housekeeping validation.
    '''''''''''''''''''''''''''''''''''''''''''''
    If FromVBProject Is Nothing Then
        CopyModule = False
        Exit Function
    End If
    
    If Trim(ModuleName) = vbNullString Then
        CopyModule = False
        Exit Function
    End If
    
    If ToVBProject Is Nothing Then
        CopyModule = False
        Exit Function
    End If
    
    If FromVBProject.Protection = vbext_pp_locked Then
        CopyModule = False
        Exit Function
    End If
    
    If ToVBProject.Protection = vbext_pp_locked Then
        CopyModule = False
        Exit Function
    End If
    
    On Error Resume Next
    Set VBComp = FromVBProject.VBComponents(ModuleName)
    If Err.Number <> 0 Then
        CopyModule = False
        Exit Function
    End If
    Err.Clear

    ''''''''''''''''''''''''''''''''''''''''''''''''''''
    ' FName is the name of the temporary file to be
    ' used in the Export/Import code.
    ''''''''''''''''''''''''''''''''''''''''''''''''''''
    FName = Environ("Temp") & "\" & ModuleName & ".bas"
    If OverwriteExisting = True Then
        ''''''''''''''''''''''''''''''''''''''
        ' If OverwriteExisting is True, Kill
        ' the existing temp file and remove
        ' the existing VBComponent from the
        ' ToVBProject.
        ''''''''''''''''''''''''''''''''''''''
        If Dir(FName, vbNormal + vbHidden + vbSystem) <> vbNullString Then
            Err.Clear
            Kill FName
            If Err.Number <> 0 Then
                CopyModule = False
                Exit Function
            End If
        End If
        With ToVBProject.VBComponents
            .Remove .Item(ModuleName)
        End With
    Else
        '''''''''''''''''''''''''''''''''''''''''
        ' OverwriteExisting is False. If there is
        ' already a VBComponent named ModuleName,
        ' exit with a return code of False.
        ''''''''''''''''''''''''''''''''''''''''''
        Err.Clear
        Set VBComp = ToVBProject.VBComponents(ModuleName)
        If Err.Number <> 0 Then
            If Err.Number = 9 Then
                ' module doesn't exist. ignore error.
            Else
                ' other error. get out with return value of False
                CopyModule = False
                Exit Function
            End If
        End If
    End If
    
    ''''''''''''''''''''''''''''''''''''''''''''''''''''
    ' Do the Export and Import operation using FName
    ' and then Kill FName.
    ''''''''''''''''''''''''''''''''''''''''''''''''''''
    FromVBProject.VBComponents(ModuleName).Export Filename:=FName
    
    '''''''''''''''''''''''''''''''''''''
    ' Extract the module name from the
    ' export file name.
    '''''''''''''''''''''''''''''''''''''
    SlashPos = InStrRev(FName, "\")
    ExtPos = InStrRev(FName, ".")
    CompName = Mid(FName, SlashPos + 1, ExtPos - SlashPos - 1)
    
    ''''''''''''''''''''''''''''''''''''''''''''''
    ' Document modules (SheetX and ThisWorkbook)
    ' cannot be removed. So, if we are working with
    ' a document object, delete all code in that
    ' component and add the lines of FName
    ' back in to the module.
    ''''''''''''''''''''''''''''''''''''''''''''''
    Set VBComp = Nothing
    Set VBComp = ToVBProject.VBComponents(CompName)

    If DebugMensajes Then ExcelLocal.Sheets("Lineas").Cells(i, 3).Value = CompName

    If VBComp Is Nothing Then
        If DebugMensajes Then ExcelLocal.Sheets("Lineas").Cells(i, 4).Value = CompName & " normal"
        ToVBProject.VBComponents.Import Filename:=FName
    Else
        If VBComp.Type = vbext_ct_Document Then
            If DebugMensajes Then ExcelLocal.Sheets("Lineas").Cells(i, 4).Value = CompName & " reescribir"
            ' VBComp is destination module
            Set TempVBComp = ToVBProject.VBComponents.Import(FName)
            ' TempVBComp is source module
            With VBComp.CodeModule
                .DeleteLines 1, .CountOfLines
                s = TempVBComp.CodeModule.Lines(1, TempVBComp.CodeModule.CountOfLines)
                .InsertLines 1, s
            End With
            On Error GoTo 0
            ToVBProject.VBComponents.Remove TempVBComp
        Else
            MsgBox "Modulo con fallos " & CompName & " tipo " & ComponentTypeToString(VBComp.Type)
            ToVBProject.VBComponents.Import Filename:=FName
        End If
    End If
    Kill FName
    CopyModule = True
End Function

Function ComponentTypeToString(ComponentType As VBIDE.vbext_ComponentType) As String
    Select Case ComponentType
        Case vbext_ct_ActiveXDesigner
            ComponentTypeToString = "ActiveX Designer"
        Case vbext_ct_ClassModule
            ComponentTypeToString = "Class Module"
        Case vbext_ct_Document
            ComponentTypeToString = "Document Module"
        Case vbext_ct_MSForm
            ComponentTypeToString = "UserForm"
        Case vbext_ct_StdModule
            ComponentTypeToString = "Code Module"
        Case Else
            ComponentTypeToString = "Unknown Type: " & CStr(ComponentType)
    End Select
End Function

Function TotalCodeLinesInVBComponent(VBComp As VBIDE.VBComponent) As Long
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' This returns the total number of code lines (excluding blank lines and
' comment lines) in the VBComponent referenced by VBComp. Returns -1
' if the VBProject is locked.
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
    Dim N As Long
    Dim s As String
    Dim LineCount As Long
    
    If VBComp.Collection.Parent.Protection = vbext_pp_locked Then
        TotalCodeLinesInVBComponent = -1
        Exit Function
    End If
    
    With VBComp.CodeModule
        For N = 1 To .CountOfLines
            s = .Lines(N, 1)
            If Trim(s) = vbNullString Then
                ' blank line, skip it
            ElseIf Left(Trim(s), 1) = "'" Then
                ' comment line, skip it
            Else
                LineCount = LineCount + 1
            End If
        Next N
    End With
    TotalCodeLinesInVBComponent = LineCount
End Function

'===========================================================
' Barra de progreso
'===========================================================
Private Sub ActBarra()
    'Fracción por cada llamada
    frac = progreso + frac
    'Actualizar barra de progreso
    With ufProgress
        .LabelProgress.Width = frac * (.FrameProgress.Width)
        .Repaint
    End With
    DoEvents
End Sub

Private Sub IniBarra()
    'Inicializar barra de progreso
    frac = 0
    ufProgress.LabelProgress.Width = 0
    ' ufProgress.Caption = "Actualizar documento"
    ' ufProgress.LabelCaption.Caption = "Extrayendo ruta de archivo actual.." & vbCrLf & _
    '     "Por favor, no abra o seleccione otro documento de Excel o Word."
    ufProgress.TiempoEstimado.Caption = ""
    ufProgress.Show
End Sub

'===========================================================
' Funciones integradas a modulo de actualización
'===========================================================
Sub ComprobarActualizacionIntegrado(Optional modo As String)
    'On Error GoTo ErrorHandler
    'Iniciar FSO para comprobación de existencia de archivo
    Set fso = CreateObject("Scripting.FileSystemObject")
    'Conseguir versión actual de codigo
    VersionActual = VersionCodigoIntegrado()
    'Conseguir última versión de programa
    RutaLicencia = RutaUsuario & RutaBase & "\Licencias.txt"
    'Comprobar que archivo existe
    If Not fso.FileExists(RutaLicencia) Then
        'No existe
        If modo = "A" Then  'Se ha pulsado el botón de buscar actualizacion, abrir selector de carpetas
            Reintentar:
            Dim sFolder As String
            'Abir selector de carpetas
            With Application.FileDialog(msoFileDialogFolderPicker)
                .Title = "Elige la carpeta de ESCRITORIO"
                '.InitialFileName = ""
                If .Show = -1 Then 'mirar si se ha presionado el OK
                    sFolder = .SelectedItems(1)
                End If
            End With

            If sFolder <> "" Then 'se ha seleccionado una carpeta
                'Encontrar segunda instancia de \ para conseguir ruta relativa por usuario
                If RutaRelativa(sFolder) <> "" Then
                    RutaUsuario = RutaRelativa(sFolder)
                Else
                    'No se ha encontrado un \, se ha elegido una carpeta no válida
                    vbOut = MsgBox("La carpeta que se ha elegido no es válida.", vbRetryCancel + vbExclamation, "Carpeta no válida")

                    If vbOut = 4 Then GoTo Reintentar   'Reintentar
                    If vbOut = 2 Then End   'Cancelar
                End If
                'Establecer nueva ruta a partir de la ruta relativa
                RutaLicencia = RutaUsuario & RutaBase & "\Licencias.txt"
                'MsgBox RutaLicencia
                If Not fso.FileExists(RutaLicencia) Then
                    'Sigue sin encontrarse la licencia
                    errorConexion = True
                End If
            Else
                'No se ha seleccionado carpeta
                errorConexion = True
            End If
        Else
            'MsgBox "No se ha podido establecer comunicación con el servidor, no se podrá actualizar la hoja a la versión más reciente.", vbExclamation, "Error de conexión al servidor"
            errorConexion = True
        End If
        'Limpiar objetos
        Set fso = Nothing
        Exit Sub
    End If
    errorConexion = False
    'Limpiar objetos
    Set fso = Nothing
    'Obtener número de versión y fecha de servidor
    Call ObtenerVersionActualizadaIntegrado(RutaLicencia, Proyecto)
    If VersionActual <> NumVersion Then
        'Hay una actualizacion disponible
        actualizado = False
    Else
        'Ya está actualizado
        actualizado = True
    End If
Exit Sub

ErrorHandler:
    'Si ocurre algun error ejecutar
    MsgBox "Se ha producido un error a la hora de actualizar el excel: " & Err.Description, vbOKOnly + vbExclamation, "Error 102"
    Set fso = Nothing
    Application.ScreenUpdating = True
    'Proteger
    'principal.Protect Password:=contrasenaPaginas
    'Descargar barra
    Unload ufProgress
    End
End Sub

Function VersionCodigoIntegrado() As String
    Set VBProj = ThisWorkbook.VBProject

    'Acceder al modulo del excel
    Dim vbMod As Object
    Set vbMod = VBProj.VBComponents("ThisWorkbook").CodeModule
    Dim lineNum As Long
    lineNum = 1
    Dim codeLine As String
    Do While lineNum <= vbMod.CountOfLines
        codeLine = vbMod.Lines(lineNum, 1)
        If InStr(codeLine, "Version:") > 0 Then
            'Extrae numero de string sin puntos ni espacios
            Dim version As String
            version = Trim(Replace(codeLine, "'", ""))
            version = Replace(version, "Version:", "")
            version = Trim(version)
            versionPuntos = version
            version = Replace(version, ".", "")
            'Añadir 0 a la izquierda para formato de version
            Do While Len(version) < 6
                version = "0" & version
            Loop
            VersionCodigoIntegrado = version
            If DebugMensajes Then
                MsgBox "Version codigo: " & version
                MsgBox "Version codigo: " & versionPuntos
            End If
            Exit Function
        End If
        lineNum = lineNum + 1
    Loop
    VersionCodigoIntegrado = "000000" 'Valor por defecto si no se encuentra
End Function

Sub ObtenerVersionActualizadaIntegrado(ruta As String, NumProyecto As Long)
    Dim fileNum As Integer
    Dim textLine As String
    Dim lineCounter As Long
    Dim startPos As Long
    Dim spacePosBefore As Long

    'Abrir archivo de licencia
    fileNum = FreeFile
    Open ruta For Input As #fileNum
    'Escanear archivo de licencia por el proyecto afectado
    lineCounter = 0
    Do While Not EOF(fileNum)
        Line Input #fileNum, textLine
        lineCounter = lineCounter + 1
        'Linea es la que se busca por proyecto
        If lineCounter = NumProyecto Then
            'Encontrar separador
            startPos = InStr(textLine, "-")
            If startPos > 0 Then
                'Conseguir numero de versión
                NumVersion = Mid(textLine, startPos + 1)
                'Buscar segundo separador para fecha de version
                spacePosBefore = InStrRev(Left(textLine, startPos - 1), " ")
                If spacePosBefore > 0 Then
                    'Conseguir fecha de version
                    FechaVersion = Mid(textLine, spacePosBefore + 1, startPos - spacePosBefore - 1)
                Else
                    FechaVersion = "No hay fecha de versión"
                End If
                If DebugMensajes Then
                    MsgBox "Fecha de la versión: " & FechaVersion & vbCrLf & _
                        "Número de versión: " & NumVersion
                End If
            Else
                MsgBox "No se ha encontrado número de licencia o se ha modificado el archivo." & NumProyecto, vbCritical
                Exit Sub
            End If
            Exit Do
        End If
    Loop
    'Cerrar archivo de licencia
    Close #fileNum
End Sub

'===========================================================
' Funciones ayudantes
'===========================================================
Function RutaRelativa(s As String) As String
    Dim pos As Long, pos2 As Long
    pos = InStr(s, "\") ' Encontrar primera instancia de \
    pos2 = InStr(pos + 1, s, "\") ' Encontrar segunda instancia de \
    pos = InStr(pos2 + 1, s, "\") ' Encontrar tercera instancia de \
    If pos > 0 Then
        RutaRelativa = Left(s, pos - 1)
    Else
        RutaRelativa = "" ' Si no se encuentra \ devolver ""
    End If
End Function