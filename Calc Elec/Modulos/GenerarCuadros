Option Explicit

Public ContinuarEjecucion As Boolean
Public salirMacr As Boolean
Public dict As Object
Public CorregirAutomatico As Boolean
Public CorregirManual As Boolean
Public NoCmpt As Collection
Public cambios As Object

Dim i As Double, j As Double, k As Double, ultFila As Double, n As Double, b As Double
Dim OutPut As Integer
Dim principal As Worksheet, ws As Worksheet, primerPag As Worksheet, ultPag As Worksheet
Dim buttonCell As Range, valores As Range, columnNom As Range
Dim DebugMensajes As Boolean
Dim rutas() As String

Sub MeterDict(ByVal rng As Range, ByVal dict As Object, ByVal nombre As String)
    Dim cell As Variant
    Dim name As String, addr As String

    'Introducir valores en diccionario
    For Each cell In rng
        name = Trim(cell.value)   'Nombre líena
        addr = cell.Address       'Dirección nombre
        If Len(name) > 0 Then
            AddRecord dict, name, addr, nombre  'Añadir a diccionario
        End If
    Next cell
    ' Cleanup: Unload forms and release objects
    ' On Error Resume Next
    ' Unload ufProgressObr
    ' Set principal = Nothing
    ' Set ws = Nothing
    ' Set primerPag = Nothing
    ' Set ultPag = Nothing
    ' Set buttonCell = Nothing
    ' Set valores = Nothing
    ' Set columnNom = Nothing
    ' Set dict = Nothing
End Sub

Sub ComprobarRepetidos(ByVal circuitos As Boolean, Optional autom As String)
    Dim i As Double, j As Double, a As Double, colCount As Long, OutPut As Integer
    Dim rango() As Variant, rowArr() As Variant, resultArr() As Variant
    Dim rowList As Collection
    Dim tablaPrinc As ListObject

    Set principal = ThisWorkbook.Sheets("Configuracion_cuadros")
    Set tablaPrinc = principal.ListObjects("Nombre_cuadros")
    Set valores = tablaPrinc.DataBodyRange
    rango = valores.value

    Set rowList = New Collection
    colCount = UBound(rango, 2)

    'Comprobar que cada fila tiene información necesaria
    a = 0

    For i = 1 To UBound(rango, 1)
        'Reemplazar espacios en abreviatura por barras bajas
        rango(i, 2) = Replace(rango(i, 2), " ", "_")
        'Si hay un nombre de cuadro escrito, mirar si tiene su abreviatura
        If rango(i, 1) <> "" Or rango(i, 2) <> "" Then
            ReDim rowArr(1 To colCount)
            For j = 1 To colCount
                rowArr(j) = rango(i, j)
            Next j
            rowList.Add rowArr
        ElseIf rango(i, 2) <> "" Then
            a = a + 1
        End If
    Next i

    'Convertir colección a array de dos dimensiones
    ReDim resultArr(1 To rowList.count, 1 To colCount)
    
    For i = 1 To rowList.count
        For j = 1 To colCount
            resultArr(i, j) = rowList(i)(j)
        Next j
    Next i

    Erase rango 'Limpiar
    rango = resultArr   'Volver a definir sin filas vacías

    'Cuantos se repiten
    n = ContarRepetidos(rango)

    'Redimensionar tabla
    Set valores = principal.Range(principal.Cells(3, 4), principal.Cells(UBound(rango, 1) + 3, UBound(rango, 2) + 3))
    tablaPrinc.Resize valores
    'Pegar array en tabla
    tablaPrinc.DataBodyRange.Resize(UBound(rango, 1), UBound(rango, 2)).value = rango

    Call ActualizarRutas

    If circuitos Then   'Comprobar repetidos en denominaciones de circuitos
        'Cargar todos los nombres
        Set dict = CreateObject("Scripting.Dictionary")
        j = 0
        For i = 1 To UBound(rango, 1)
            'Mirar si existe la página a calcular
            If ExistePagina(CStr(rango(i, 2))) Then
                Set columnNom = ThisWorkbook.Sheets(rango(i, 2)).ListObjects(rango(i, 2)).ListColumns(1).DataBodyRange
                Call MeterDict(columnNom, dict, CStr(rango(i, 2)))
            End If
        Next
        b = 0
        'Contar repetidos dentro de cuadros
        If ContarRepetidos2(dict) > 0 Then
            b = 1   'Marcador de denominaciones de línea repetidos
        End If
    End If

    'Limpiar
    Erase rango
    Erase rowArr
    Erase resultArr
    Set rowList = Nothing
    Set tablaPrinc = Nothing

    If autom = "A" Then Exit Sub

    'Aviso si falta alguna abreviatura
    ContinuarEjecucion = False
    salirMacr = False
    If a > 0 Then
        OutPut = MsgBox("Falta uno o mas de una abreviatura para los cuadros. Tiene que existir una abreviatura única por cuadro." & vbCrLf & "Corregir manualmente?", vbCritical + vbYesNoCancel, "Falta abreviatura")
        Select Case OutPut
            Case 6  'Si
                ufFaltan.Show vbModeless
                Do While ContinuarEjecucion = False 'Loop hasta que se cierre formulario
                    DoEvents
                Loop
                If salirMacr Then GoTo Salir
                Exit Sub    'Continuar
        End Select
        GoTo Salir
    ElseIf n > 0 Then
        OutPut = MsgBox("Una o más abreviaturas establecidas están repetidas. Tiene que existir una abreviatura única por cuadro." & vbCrLf & "Corregir manualmente?", vbCritical + vbYesNoCancel, "Abreviatura repetida")
        Select Case OutPut
            Case 6  'Si
                ufRepetidos.Show vbModeless
                Do While ContinuarEjecucion = False 'Loop hasta que se cierre formulario
                    DoEvents
                Loop
                If salirMacr Then GoTo Salir
                Exit Sub    'Continuar
        End Select
        GoTo Salir
    ElseIf b > 0 Then
        OutPut = MsgBox("Una o más denominaciones de línea establecidas están repetidas. Tiene que existir una abreviatura única por línea." & vbCrLf & "Corregir manualmente?", vbCritical + vbYesNoCancel, "Denominacion de línea repetida")
        Select Case OutPut
            Case 6  'Si
                ufRepetidos.Show vbModeless
                Do While ContinuarEjecucion = False 'Loop hasta que se cierre formulario
                    DoEvents
                Loop
                If salirMacr Then GoTo Salir
                Exit Sub    'Continuar
        End Select
        GoTo Salir
    End If
    Exit Sub
Salir:
    Unload ufProgressObr
    'Activar actualización de pantalla
    Application.ScreenUpdating = True
    'Activar calculacion automatica
    Application.Calculation = xlCalculationAutomatic
    Application.DisplayAlerts = True
    End
End Sub

' VerificarNombres
' Purpose: Checks the validity of user-defined names for compatibility in Excel, optionally in automatic mode.
' Parameters:
'   autom (Optional String): If set to "A", runs in automatic mode; otherwise, prompts user for corrections.
Sub VerificarNombres(Optional autom As String)
    DebugMensajes = ThisWorkbook.Sheets("Busquedas").Range("Debug")

    Dim rangoTestNombres As Range, valores As Range
    Dim nombreRango As String
    Dim Abreviaturas() As Variant
    Dim exst As Boolean

    Set NoCmpt = New Collection

    Set rangoTestNombres = ThisWorkbook.Sheets("Busquedas").Range("AG1")    'Rango para testear

    'Guardar valores establecidos por usuario en array
    Set valores = ThisWorkbook.Sheets("Configuracion_cuadros").ListObjects("Nombre_cuadros").DataBodyRange
    Abreviaturas = valores.value    'Array

    Call LimpiarNombresDeCelda(ThisWorkbook.Sheets("Busquedas"), "AG1")

    MsgBox "Verificando. " & UBound(Abreviaturas, 1) & " nombres a comprobar.", vbInformation, "Iniciando verificación de nombres"

    For i = 1 To UBound(Abreviaturas, 1)
        nombreRango = Abreviaturas(i, 2)

        exst = ExisteRango(nombreRango)
        'Si existe y no hay una página con ese nombre, marcar como no compatible
        If exst And Not ExistePagina(nombreRango) Then
            MsgBox "No válido"
            NoCmpt.Add nombreRango
            GoTo Siguiente
        End If

        On Error Resume Next
        ThisWorkbook.Names.Add Name:=nombreRango, RefersTo:=rangoTestNombres

        If Err.Number <> 0 And Not exst Then    'Si da error y no existe el nombre
            NoCmpt.Add nombreRango
            Err.Clear
        Else
            'Quitar nombre de prueba
            ThisWorkbook.Names(nombreRango).Delete
        End If
        On Error GoTo 0 'Restaurar manejo de errores
        Siguiente:
    Next i

    'Si hay nombres no compatibles, pedir corregir
    If NoCmpt.count = 0 Then Exit Sub 'Si no hay errores, salir
    
    Set cambios = CreateObject("Scripting.Dictionary")      'Diccionario para guardar cambios realizados

    ContinuarEjecucion = False
    salirMacr = False
    CorregirAutomatico = False
    CorregirManual = False

    If autom = "A" Then
        ufInvalidos.Show vbModeless
        Do While ContinuarEjecucion = False 'Loop hasta que se cierre formulario
            DoEvents
        Loop
        If salirMacr Then GoTo Terminar
    Else
        ufInvalidos.Show vbModeless
        Do While ContinuarEjecucion = False 'Loop hasta que se cierre formulario
            DoEvents
        Loop
        If salirMacr Then GoTo Terminar
    End If
    MsgBox "continuado"
    'Si se ha elegido automatico, generar nuevos nombres
    If CorregirAutomatico Then
        Dim nuevoNom As String, contador As Long
        For i = 1 To NoCmpt.count
            nuevoNom = NoCmpt(i)
            'Poner punto bajo en lugar de espacios
            nuevoNom = Replace(nuevoNom, " ", ".")
            'Poner guión bajo al final
            nuevoNom = nuevoNom & "_"
            'Generar nuevo nombre añadiendo un sufijo numérico hasta que no exista (si la barra baja no ha funcionado)
            contador = 1
            Do While ExisteRango(nuevoNom)
                nuevoNom = NoCmpt(i) & "_" & Format(contador, "00")
                contador = contador + 1
            Loop
            MsgBox "El nombre " & NoCmpt(i) & " se ha cambiado a " & nuevoNom, vbInformation, "Nombre corregido"
            'Guardar cambio en colección
            cambios.Add NoCmpt(i), nuevoNom
        Next i
    End If

    'Revisar abreviaturas y corregir
    Dim nuevoNombre As String
    For i = 1 To UBound(Abreviaturas, 1)
        'Comprobar si el nombre está en la lista de no compatibles
        If cambios.Exists(Abreviaturas(i, 2)) Then
            nuevoNombre = cambios(Abreviaturas(i, 2))
            'Si existe un cambio, aplicarlo
            Abreviaturas(i, 2) = nuevoNombre
        End If
    Next i
    'Mostrar resumen de cambios
    Dim textReport  As String
    textReport = ""
    For i = 1 To UBound(Abreviaturas, 1)
        textReport = textReport & " " & Abreviaturas(i, 2) & _
            IIf(i = cambios.count, ".", ",")
    Next i

    MsgBox textReport, vbInformation, "Nombres de cuadro corregidos"

    'Actualizar abreviaturas en la tabla
    Set valores = ThisWorkbook.Sheets("Configuracion_cuadros").ListObjects("Nombre_cuadros").DataBodyRange

    valores.Columns(2).value = Application.WorksheetFunction.Index(Abreviaturas, 0, 2)   'Extraer segunda columna de array
    Call ActualizarRutas
    Exit Sub
Terminar:
    'Desactivar actulaizaciones de pantalla
    Application.ScreenUpdating = True
    'Desactivar calculacion manual
    Application.Calculation = xlCalculationAutomatic
    Application.EnableEvents = True
    Application.DisplayAlerts = True
End Sub

Sub LimpiarNombresDeCelda(ws As Worksheet, targetAddress As String)
    Dim nm As Name
    Dim ref As String
    
    ' Normalize address to absolute format
    targetAddress = ws.Range(targetAddress).Address(True, True)
    
    On Error Resume Next
    For Each nm In ThisWorkbook.Names
        ' Some names can be hidden or invalid, so we need to catch errors
        ref = Replace(nm.RefersTo, "=", "")
        
        ' Compare normalized reference
        If StrComp(ref, ws.Name & "!" & targetAddress, vbTextCompare) = 0 Then
            nm.Delete
        End If
    Next nm
    On Error GoTo 0
End Sub

Sub Cuadros(Optional autom As String)
    DebugMensajes = ThisWorkbook.Sheets("Busquedas").Range("Debug")
    If Not DebugMensajes Then On Error GoTo ErrorHandler

    Set principal = ThisWorkbook.Sheets("Configuracion_cuadros")
    ultFila = principal.Cells(principal.Rows.count, "D").End(xlUp).row
    
    k = ultFila - 3

    If autom = "A" Then

    Else
        'Inicializar barras de progreso
        Call IniciarBarra
        progreso = 1 / (k + 1)
        'Desactivar actulaizaciones de pantalla
        Application.ScreenUpdating = False
        'Desactivar calculacion manual
        Application.Calculation = xlCalculationManual
        'Borrar portapapeles
        Application.CutCopyMode = False
        Application.EnableEvents = False
        Application.DisplayAlerts = False
    End If

    'Cuantas veces se llama a actualizar barra
    progreso2 = 1 / (1)
    ufProgressObr.SubProceso.Caption = "Verificando validez de datos.."

    Call ComprobarRepetidos(False, autom)

    Call VerificarNombres(autom)

    Call ActualizarBarra2
    Call ReiniciarBarra2

    i = 1
    ufProgressObr.Proceso.Caption = "Generando cuadro número " & i & " de " & k
    ufProgressObr.SubProceso.Caption = "Haciendo copia de plantilla.."
    'Generar cuadros necesarios
    j = 1
    n = 0
    For i = 4 To k + 3
        If Cerrar Then Call CerrarGenerador
        'Generar cuadro sólo si dispone de nombre y abreviatura y no existe la página
        If principal.Cells(i, 4).value <> "" And principal.Cells(i, 5).value <> "" And Not ExistePagina(principal.Cells(i, 5).value) Then
            ufProgressObr.TituloObra.Caption = principal.Cells(i, 4).value
            ufProgressObr.Proceso.Caption = "Generando cuadro número " & i - 3 & " de " & k
            progreso2 = 1 / 2
            'Copiar plantilla
            With Sheets("PLANTILLA_CUADROS")
                .Select
                .Copy After:=Sheets(j)
                j = j + 1
            End With
            Sheets("PLANTILLA_CUADROS (2)").Select
            ufProgressObr.SubProceso.Caption = "Haciendo copia de plantilla.."
            Call ActualizarBarra2
            'Poner datos
            With Sheets("PLANTILLA_CUADROS (2)")
                'Nombre de cuadro
                .Range("E2").value = principal.Cells(i, 4).value
                'Denominación
                .Range("A4").value = principal.Cells(i, 5).value & ".01"
            End With
            ufProgressObr.SubProceso.Caption = "Poniendo datos de cuadro.."
            Call ActualizarBarra2
            'Nombre de tabla
            ActiveSheet.ListObjects(1).name = principal.Cells(i, 5).value
            'Cambiar nombre de página
            Sheets("PLANTILLA_CUADROS (2)").name = principal.Cells(i, 5).value
            ufProgressObr.SubProceso.Caption = "Cambiando nombre.."
            'Última página generada
            Set ultPag = ActiveSheet
            n = n + 1
            Call ReiniciarBarra2
        End If
    Next i
    Call ActualizarRutas
    'Borrar botones de anterior y siguiente
    ufProgressObr.TituloObra.Caption = ""
    ufProgressObr.Proceso.Caption = "Borrando botones."
    ufProgressObr.SubProceso.Caption = ""
    If n > 0 Then   'Si se ha generado alguna página
        'Primera página
        principal.Select
        Set ws = ActiveSheet
        Set primerPag = ws.Next
        'Borrar el boton de anterior
        primerPag.Select
        Set buttonCell = ActiveSheet.Range("H1")
        'Si existe un boton en la celda borrar
        Call BorrarBoton(buttonCell)
        'Borrar el boton de siguiente
        ultPag.Select
        Set buttonCell = ActiveSheet.Range("J1")
        'Si existe un boton en la celda borrar
        Call BorrarBoton(buttonCell)
    End If
    ufProgressObr.Proceso.Caption = "Finalizando macro."

    If autom = "A" Then

    Else
        principal.Select
        'Activar actulaizaciones de pantalla
        Application.ScreenUpdating = True
        'Activar calculacion automatica
        Application.Calculation = xlCalculationAutomatic
        Application.EnableEvents = True
        Application.DisplayAlerts = True
        MsgBox "Se han generado " & j - 1 & " cuadros correctamente", vbInformation, "Macro completado sin errores"
        Unload ufProgressObr
    End If

    Exit Sub
ErrorHandler:
    'Si ocurre algun error ejecutar
    MsgBox "Ha ocurrido un error al generar los cuadros solicitados: " & Err.Description, vbExclamation, "Error 201"
        'Activar actulaizaciones de pantalla
        Application.ScreenUpdating = True
        'Activar calculacion automatica
        Application.Calculation = xlCalculationAutomatic
        Application.EnableEvents = True
        Application.DisplayAlerts = True
    'Borrar copia si existe
    If ExistePagina("PLANTILLA_CUADROS (2)") Then
        Application.DisplayAlerts = False
        ThisWorkbook.Sheets("PLANTILLA_CUADROS (2)").Select
        ActiveWindow.SelectedSheets.Delete
        Application.DisplayAlerts = True
    End If
    Unload ufProgressObr
End Sub

'================================================
'Subs ayudantes
'================================================
Sub ActualizarRutas()
    Dim cuadrs() As Variant, valores As Range
    Dim princ As Worksheet
    Dim tabla As ListObject

    Set princ = ThisWorkbook.Sheets("Configuracion_cuadros")
    Set tabla = princ.ListObjects("Nombre_cuadros")
    Set valores = tabla.DataBodyRange
    cuadrs = valores.value

    For i = 1 To UBound(cuadrs, 1)
        'Poner ruta a pagina de cuadro en el nombre
        princ.Select
        'Agregar hipervínculo directamente a la celda sin seleccionar
        princ.Hyperlinks.Add Anchor:=tabla.ListColumns(1).DataBodyRange.Cells(i), Address:="", SubAddress:= _
            "'" & cuadrs(i, 2) & "'!A4", TextToDisplay:=tabla.ListColumns(1).DataBodyRange.Cells(i).Text
        'Formato fuente
        With tabla.ListColumns(1).DataBodyRange.Cells(i).Font
            .Underline = xlUnderlineStyleNone
            .ColorIndex = xlAutomatic
            .TintAndShade = 0
        End With
    Next i
End Sub

Private Sub CerrarGenerador()
    'Cerrar macro correctamente
    'Activar actualización de pantalla
    Application.ScreenUpdating = True
    'Activar calculacion automatica
    Application.Calculation = xlCalculationAutomatic
    Application.DisplayAlerts = True
    'Borrar copia si existe
    If ExistePagina("PLANTILLA_CUADROS (2)") Then
        Application.DisplayAlerts = False
        ThisWorkbook.Sheets("PLANTILLA_CUADROS (2)").Select
        ActiveWindow.SelectedSheets.Delete
        Application.DisplayAlerts = True
    End If
    principal.Select
    End
End Sub

Sub BorrarBoton(cell As Range)
    Dim shp As Shape
    For Each shp In cell.Parent.Shapes
        If Not Intersect(shp.TopLeftCell, cell) Is Nothing Then
            shp.Delete
            Exit Sub
        End If
    Next shp
End Sub

'================================================
'Funciones ayudantes
'================================================
Function ExistePagina(nombrePag As String, Optional wb As Workbook) As Boolean
    Dim test As Worksheet
    If wb Is Nothing Then
        Set wb = ThisWorkbook 'Usar el libro actual
    End If
    On Error GoTo ErrorHandler
    Set test = wb.Sheets(nombrePag)
    ExistePagina = True
    Exit Function
ErrorHandler:
    'Ha ocurrido un error al abrir la pagina, no existe
    ExistePagina = False
End Function

Function ContarRepetidos(rng As Variant) As Long
    Set dict = CreateObject("Scripting.Dictionary")
    
    Dim val As Variant
    Dim repeats As Long
    repeats = 0

    'Contar ocurrencias
    For i = LBound(rng, 1) To UBound(rng, 1)
        val = Trim(rng(i, 1))
        If Len(val) > 0 Then
            If dict.Exists(val) Then
                dict(val) = dict(val) + 1
            Else
                dict.Add val, 1
            End If
        End If
    Next i
    
    'Contar valores que se encuentras mas de una vez.
    Dim key As Variant
    For Each key In dict.Keys
        If dict(key) > 1 Then
            repeats = repeats + 1
        End If
    Next key
    
    ContarRepetidos = repeats
End Function

Function ContarRepetidos2(dict As Object) As Long
    'Contar valores que se encuentras mas de una vez.
    Dim repeats As Long
    Dim rec As Variant, col As Collection
    Dim key As Variant

    For Each key In dict.Keys
        Set col = dict(key)
        If col.count > 1 Then   'Más de una fila en la colección
            repeats = repeats + 1
            ' Debug.Print "Duplicate Name: " & key
            ' Dim i As Long
            ' For i = 1 To col.Count
            '     rec = col(i)
            '     Debug.Print "  Addr=" & rec(0) & "  Sheet=" & rec(1)
            ' Next i
        End If
    Next key

    ContarRepetidos2 = repeats
End Function

Private Sub AddRecord(dict As Object, name As String, addr As String, wsName As String)
    Dim col As Collection, rec(0 To 1) As String
    'Construir array de ubicación de nombre
    rec(0) = addr
    rec(1) = wsName
    
    If dict.Exists(name) Then   'Añadir array a colección por nombre
        dict(name).Add rec
    Else    'Generar nueva colección para guardar futura repetición
        Set col = New Collection
        col.Add rec
        dict.Add name, col
    End If
End Sub

Function ExisteRango(nom As String) As Boolean
    'Comprueba si existe un nombre de rango en el libro
    On Error GoTo Nocompatible
    ExisteRango = Evaluate("IsRef(" & nom & ")")
    Exit Function
Nocompatible:
    ExisteRango = False
    On Error GoTo 0
End Function