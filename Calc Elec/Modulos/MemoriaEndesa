Dim fso As Object, f As Object

Sub MemoriaEndesa()
    Dim pdfPath As String, folderPath As String, imgPath As String
    Dim fDialog As FileDialog, shellCmd As String
    Dim page As Long, imgFile As String
    Dim wrdApp As Object, wrdDoc As Object, wrdRange As Object

    Dim fileList As Collection
    Set fileList = New Collection
    Dim tempArr() As String
    Dim i As Long, n As Long

    ' Ask user for PDF file
    Set fDialog = Application.FileDialog(msoFileDialogFolderPicker)
    Set fso = CreateObject("Scripting.FileSystemObject")
    
    fDialog.Title = "Elegir carpeta de imagenes PDF"
    If fDialog.Show <> -1 Then Exit Sub
    folderPath = fDialog.SelectedItems(1)

    ' Open Word
    Set wrdApp = CreateObject("Word.Application")
    wrdApp.Visible = True
    Set wrdDoc = wrdApp.Documents.Add

    For Each f In fso.GetFolder(folderPath).Files
        If LCase(fso.GetExtensionName(f.name)) = "png" Then
            fileList.Add f.path
        End If
    Next

    ' Convert to array
    n = fileList.count
    ReDim tempArr(1 To n)

    For i = 1 To n
        tempArr(i) = fileList(i)
    Next

    ' --- Sort array by page number ---
    Call SortByPageNumber(tempArr)

    ' --- Insert sorted pages into Word ---
    For i = 1 To UBound(tempArr)
        wrdDoc.Content.InsertAfter vbCrLf
        wrdApp.Selection.InlineShapes.AddPicture tempArr(i), False, True
        wrdApp.Selection.InsertBreak 7  ' Page break
    Next
End Sub

Sub SortByPageNumber(arr() As String)
    Dim i As Long, j As Long
    Dim temp As String
    Dim numI As Long, numJ As Long

    For i = LBound(arr) To UBound(arr) - 1
        For j = i + 1 To UBound(arr)
            
            numI = ExtractPageNumber(arr(i))
            numJ = ExtractPageNumber(arr(j))
            
            If numI > numJ Then
                temp = arr(i)
                arr(i) = arr(j)
                arr(j) = temp
            End If
        Next j
    Next i
End Sub

Function ExtractPageNumber(path As String) As Long
    Dim file As String
    Dim regex As Object
    Dim matches As Object

    file = fso.GetFileName(path)

    Set regex = CreateObject("VBScript.RegExp")
    regex.Pattern = "(\d+)"
    regex.Global = False

    If regex.test(file) Then
        Set matches = regex.Execute(file)
        ExtractPageNumber = CLng(matches(0).Value)
    Else
        ExtractPageNumber = 0
    End If
End Function