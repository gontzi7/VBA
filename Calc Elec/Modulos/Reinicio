Option Explicit

Dim OutPut As Integer
Dim tabl As ListObject, tablaALIM As ListObject
Dim DebugMensajes As Boolean, Ignorar As Boolean
Dim nomCuadro As String
Dim n As Long, ultFila As Long
Dim Apoyo As Worksheet
Dim principal As Worksheet

Sub Reiniciar(Optional autom As String)
    DebugMensajes = ThisWorkbook.Sheets("Busquedas").Range("Debug")
    If Not DebugMensajes Then On Error GoTo ErrorHandler

    Set tablaALIM = ThisWorkbook.Sheets("ALIM").ListObjects("ALIM")
    If autom = "A" Then
        OutPut = 6
    Else
        'Mensaje de autom
        OutPut = MsgBox("Si se reinicia el libro se borrarán todos los datos introducidos, continuar?", vbYesNoCancel + vbExclamation, "Confirmación reinicio")
        Call IniciarBarra
        'Textos
        ufProgressObr.Caption = "Reiniciar libro"
        ufProgressObr.Proceso.Caption = "Formateando tabla de resultados.."
        'Cuantas veces se llama a actualizar barra
        progreso = 1 / 3
    End If

    Select Case OutPut
        Case 6  'Si
            'Desactivar actualización de pantalla
            Application.ScreenUpdating = False
            'Desactivar calculacion automatica
            Application.Calculation = xlCalculationManual

            'Borrar resultados
            Call FormatearReslt("I")
            Call ReiniciarBarra2
    
            ufProgressObr.Proceso.Caption = "Borrando cuadros de libro.."
            'Borrar cuadros
            Call BorrarCuadros("A")
            ufProgressObr.Proceso.Caption = "Formateando tabla principal.."
            'Borrar tabla ALIM
            If tablaALIM.ListRows.count > 1 Then tablaALIM.DataBodyRange.Offset(1).Resize(tablaALIM.ListRows.count - 1).Rows.Delete
            'Borrar tabla principal
            Call FormatearPrincipal("I")
            Call ReiniciarBarra2
        Case 7  'No
            Exit Sub
        Case 2  'Cancelar
            Exit Sub
        Case Else   'Otra cosa que pueda pasar
            Exit Sub
    End Select

    If autom <> "A" Then Unload ufProgressObr

    Exit Sub
ErrorHandler:
    'Si ocurre algun error ejecutar
    MsgBox "Ha ocurrido un error al actualizar tablas en resumen de resultados: " & Err.Description, vbExclamation, "Error 601"
    'Activar actualización de pantalla
    Application.ScreenUpdating = True
    'Activar calculacion automatica
    Application.Calculation = xlCalculationAutomatic
    Application.DisplayAlerts = True
    Unload ufProgressObr
End Sub

Sub FormatearPrincipal(Optional ignr As String)
    On Error Resume Next 'Por si ya tiene una única fila

    Set tabl = ThisWorkbook.Sheets("Configuracion_cuadros").ListObjects("Nombre_cuadros")

    If ignr = "I" Then
        Ignorar = True
    Else
        Ignorar = False
    End If
    'Verificar que no hay datos en tabla de resultados, sino, avisar
    If tabl.ListRows.count > 1 And Not Ignorar Then
        OutPut = MsgBox("Se ha detectado que hay datos en la tabla principal, si continua, se borrarán permanentemente los datos introducidos." & vbCrLf & _
            "Proceder a formatear?", vbExclamation + vbYesNoCancel, "Detectados datos existentes en tabla principal")
        If OutPut = 7 Then GoTo Salir1
        If OutPut = 2 Then GoTo Salir1
        Call IniciarBarra
        'Textos
        ufProgressObr.Caption = "Formatear tabla principal"
        ufProgressObr.Proceso.Caption = "Formateando tabla principal.."
        progreso = 1
    End If
    progreso2 = 1 / 3
    Call ActualizarBarra2

    ufProgressObr.SubProceso.Caption = "Eliminando todas las filas menos una.."
    'Dejar una única fila en la tabla de resultados
    tabl.DataBodyRange.Offset(1).Resize(tabl.DataBodyRange.Rows.count - 1).Rows.Delete
    Call ActualizarBarra2
    On Error GoTo 0
    
    ufProgressObr.SubProceso.Caption = "Eliminando formateo de tabla.."
    'Quitar formato de celda
    With tabl.DataBodyRange.Font
        .name = "Arial"
        .Size = 10
        .Strikethrough = False
        .Superscript = False
        .Subscript = False
        .OutlineFont = False
        .Shadow = False
        .Underline = xlUnderlineStyleNone
        .Italic = False
        .Bold = False
        .TintAndShade = 0
        .ThemeFont = xlThemeFontNone
    End With
    Call ActualizarBarra2

    ufProgressObr.SubProceso.Caption = "Eliminando datos escritos por defecto.."
    tabl.ListColumns(1).DataBodyRange.Cells(1).value = ""   'Nombre cuadro
    tabl.ListColumns(2).DataBodyRange.Cells(1).value = ""   'Abreviatura
    tabl.ListColumns(3).DataBodyRange.Cells(1).value = ""   'L. Alim
    tabl.ListColumns(4).DataBodyRange.Cells(1).value = ""   'Item
    tabl.ListColumns(5).DataBodyRange.Cells(1).value = ""   'Nombre
    Call ActualizarBarra2
    Call ReiniciarBarra2

    If ignr <> "I" Then Unload ufProgressObr
    Exit Sub
Salir1:
    'Activar actualización de pantalla
    Application.ScreenUpdating = True
    'Activar calculacion automatica
    Application.Calculation = xlCalculationAutomatic
    Application.DisplayAlerts = True
    End
End Sub

Function ExistePagina(nombrePag As String, Optional wb As Workbook) As Boolean
    Dim test As Worksheet
    If wb Is Nothing Then
        Set wb = ThisWorkbook 'Usar el libro actual
    End If
    On Error GoTo ErrorHandler
    Set test = wb.Sheets(nombrePag)
    ExistePagina = True
    Exit Function
ErrorHandler:
    'Ha ocurrido un error al abrir la pagina, no existe
    ExistePagina = False
End Function

Sub BorrarCuadros(Optional autom As String)
    DebugMensajes = ThisWorkbook.Sheets("Busquedas").Range("Debug")
    If Not DebugMensajes Then On Error GoTo ErrorHandler

    Set principal = ThisWorkbook.Sheets("Configuracion_cuadros")
    If autom = "" Then
        'Mensaje de confirmacion
        OutPut = MsgBox("Se borrarán las hojas de los cuadros, continuar?", vbYesNo + vbExclamation)
        Call IniciarBarra
        'Textos
        ufProgressObr.Caption = "Borrar cuadros"
        ufProgressObr.Proceso.Caption = "Borrando cuadros de libro.."
        progreso = 1
    Else
        OutPut = 6
    End If
    If OutPut = 6 Then
        'Desactivar actulaizaciones de pantalla
        Application.ScreenUpdating = False
        'Desactivar calculacion automatica hasta despues de pegar todos los valores
        Application.Calculation = xlCalculationManual
        Application.DisplayAlerts = False
        'Número de filas en tabla
        ultFila = principal.Cells(principal.Rows.count, "D").End(xlUp).row
        progreso2 = 1 / (ultFila - 3)
        'Limpiar hojas de apoyos
        For n = 4 To ultFila
            nomCuadro = principal.Cells(n, 5).Text
            'Si no es un espacio en blanco, borrar
            If principal.Cells(n, 5).Text <> "" And ExistePagina(nomCuadro) Then
                ufProgressObr.SubProceso.Caption = "Borrando cuadro nº " & n - 3 & " de " & ultFila - 4
                Set Apoyo = ThisWorkbook.Sheets(nomCuadro)
                Apoyo.Delete
                'Quitar vinculos de titulos
                principal.Select
                principal.Cells(n, 4).Select
                Selection.Hyperlinks(1).Delete
                Call ActualizarBarra2
            End If
        Next n
        'Borrar copia si existe
        If ExistePagina("PLANTILLA_CUADROS (2)") Then
            ufProgressObr.SubProceso.Caption = "Borrando copia sin formatear.."
            ThisWorkbook.Sheets("PLANTILLA_CUADROS (2)").Select
            ActiveWindow.SelectedSheets.Delete
        End If
        ufProgressObr.SubProceso.Caption = "Finalizando eliminación de cuadros.."
        Application.DisplayAlerts = True
        principal.Select
        'Quitar validación de datos y borrar lista
        principal.ListObjects("Nombre_cuadros").ListColumns(3).DataBodyRange.Validation.Delete
        ThisWorkbook.Sheets("Busquedas").Range("AG:AG").ClearContents
        Call ReiniciarBarra2
        If autom = "" Then
            Unload ufProgressObr
            'Activar actualización de pantalla
            Application.ScreenUpdating = True
            'Activar calculacion automatica
            Application.Calculation = xlCalculationAutomatic
            Application.DisplayAlerts = True
            Application.EnableEvents = True
        End If
    Else
        'Se ha decidido no continuar con la operación
        Exit Sub
    End If
    Exit Sub
ErrorHandler:
    'Si ocurre algun error ejecutar
    MsgBox "Ha ocurrido un error al borrar las páginas de cuadros: " & Err.Description, vbExclamation, "Error 301"
    'Activar actualización de pantalla
    Application.ScreenUpdating = True
    'Activar calculacion automatica
    Application.Calculation = xlCalculationAutomatic
    Application.DisplayAlerts = True
    Application.EnableEvents = True
End Sub